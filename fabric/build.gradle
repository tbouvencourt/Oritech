plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath.set(project(":common").loom.accessWidenerPath)
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    flatDir {
        dir './libs'
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    // modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"
    modImplementation "dev.architectury:architectury-fabric:13.0.6"
    // because gradle is stupid and my local setup is broken somewhere but deleting the cache doesnt seem to fix it

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    // Energy API
    include modApi("teamreborn:energy:4.1.0") {
        transitive = false
    }

    // yes yes all this needs to be cleaned up, and recipe viewers made conditional, but I'm waiting to clean up the CSL mess first

    // owo lib
    modImplementation "io.wispforest:owo-lib:0.12.11+1.21"

    // geckolib
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.21.1:4.6.6"

    // CSL
    // include modApi("earth.terrarium.common_storage_lib:common-storage-lib-fabric-1.21:0.0.5")
    // yeah this is super weird, I have no idea why I have to list the subprojects, and why the include doesn't work here, or why the 0.0.6 jar doesnt work
    include modApi('earth.terrarium.common_storage_lib:common-storage-lib-fabric-1.21:0.0.27')
    include modApi('earth.terrarium.common_storage_lib:common-storage-lib-data-fabric-1.21:0.0.27')
    include modApi('earth.terrarium.common_storage_lib:common-storage-lib-lookup-fabric-1.21:0.0.27')
    include modApi('earth.terrarium.common_storage_lib:common-storage-lib-resources-fabric-1.21:0.0.27')
//    modApi files('common-storage-lib-fabric-1.21-0.0.8.jar')
//    modApi files('common-storage-lib-data-fabric-1.21-0.0.9.jar')
//    modApi files('common-storage-lib-resources-fabric-1.21-0.0.8.jar')
//    modApi files('common-storage-lib-lookup-fabric-1.21-0.0.8.jar')

    // include(files('common-storage-lib-fabric-1.21-0.0.8.jar'))

    // JADE (needed for addon)
    modImplementation "curse.maven:jade-324717:5639994"

    // athena (CTM)
    modRuntimeOnly "earth.terrarium.athena:athena-fabric-1.21:4.0.0"

    // EMI
    modRuntimeOnly "dev.emi:emi-fabric:1.1.12+1.21"

    // REI
    // modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:16.0.754"

    // mod menu
    modImplementation "com.terraformersmc:modmenu:11.0.3"

}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

tasks.remapJar {
    injectAccessWidener.set(true)
}

publishMods {
    file = remapJar.archiveFile
    changelog = file("../CHANGELOG.md").getText()
    type = ALPHA
    modLoaders.add("fabric")

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_API_KEY")
        projectId = "4sYI62kA"
        minecraftVersions.add("1.21.1")
        minecraftVersions.add("1.21")

        requires("owo-lib")
        requires("geckolib")
        requires("athena-ctm")
        requires("fabric-api")

        announcementTitle = "Fabric Version on Modrinth"
    }

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
        projectId = "1030830"
        minecraftVersions.add("1.21.1")
        minecraftVersions.add("1.21")

        requires("owo-lib")
        requires("geckolib")
        requires("athena")
        requires("fabric-api")

        javaVersions.add(JavaVersion.VERSION_21)
        clientRequired = true
        serverRequired = true

        projectSlug = "oritech"
        announcementTitle = "Fabric Version on CurseForge"
    }
}

